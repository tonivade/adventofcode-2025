; https://adventofcode.com/2025/day/4

(import java.lang.String lines)
(import java.lang.String split 1)
(import java.lang.Integer parseInt 1)

(load-file "mal/aoc.mal")
(load-file "mal/matrix.mal")

(defn count-paper [point matrix]
    (let* (points (adjacent point))
        (count
            (filter
                (fn* [k]
                    (and (contains? matrix k) (= (get matrix k) "@"))
                )
                points
            )
        )
    )
)

(defn removable [matrix]
    (filter
        (fn* [p] (< (count-paper (to-pair p) matrix) 4))
        (filter (fn* [k] (= (get matrix k) "@")) (keys matrix))
    )
)

(defn remove-all [matrix to-remove]
    (if (empty? to-remove)
        matrix
        (remove-all (dissoc matrix (first to-remove)) (rest to-remove))
    )
)

(defn step [matrix acc]
    (let* (to-remove (removable matrix))
        (if (empty? to-remove)
            acc
            (step (remove-all matrix to-remove) (+ acc (count to-remove)))
        )
    )
)

(def! input (if-defined DEBUG-EVAL "input/day4-test.txt" "input/day4.txt"))

(println (timed! (count (removable (parse-matrix input)))))

(println (timed! (step (parse-matrix input) 0)))
