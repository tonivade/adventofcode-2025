; https://adventofcode.com/2025/day/1

(import java.lang.String lines)
(import java.lang.String charAt 1)
(import java.lang.String substring 1)
(import java.lang.Integer parseInt 1)

(load-file "mal/aoc.mal")

(defn split-pair [s] 
    (list (charAt s 0) (parseInt (substring s 1)))
)

(defn parse [file]
    (let* (input (slurp file)) (map split-pair (lines input)))
)

(defn next [move position]
    (cond
        (= (first move) "R") (% (+ position (% (second move) 100)) 100)
        (= (first move) "L") (% (+ position (- 100 (% (second move) 100))) 100)
    )
)

(defn zeros [move position] 
    (cond
        (and (= (first move) "R") (>= (+ position (% (second move) 100)) 100)) (incr (/ (second move) 100))
        (and (= (first move) "L") (> position 0) (<= (- position (% (second move) 100)) 0)) (incr (/ (second move) 100))
        true (/ (second move) 100)
    )
)

(def! input (if-defined DEBUG-EVAL "input/day1-test.txt" "input/day1.txt"))

(defn part1 []
    (count 
        (filter (fn* [a] (= a 0)) 
            (reduce (list 50) 
                (fn* [move state] (cons (next move (first state)) state)) 
                (parse input)
            )
        )
    )
)

(defn part2 []
    (second
        (reduce (list 50 0) 
            (fn* [move state] 
                (list (next move (first state)) (+ (second state) (zeros move (first state)))))
            (parse input)
        )
    )
)

(println (timed part1))

(println (timed part2))