; https://adventofcode.com/2025/day/2

(import java.lang.String split 1)
(import java.lang.String length)
(import java.lang.String trim)
(import java.lang.String substring 2)
(import java.lang.Long parseLong 1)
(import java.lang.Long toString 1)

(load-file "mal/aoc.mal")

(defn split-pair [s] 
    (map parseLong (split (trim s) "-"))
)

(defn parse [file]
    (let* (input (slurp file)) 
        (map split-pair (split input ","))
    )
)

(defn is-repeated? [s]
    (let* (i (/ (length s) 2)) 
        (= (substring s 0 i) (substring s i (length s)))
    )
)

(defn is-invalid? [number]
    (let* (s (toString number)) 
        (and (= (% (length s) 2) 0) (is-repeated? s))
    )
)

(defn unfold1 [range]
    (filter is-invalid? (generate (first range) (second range)))
)

(defn unfold1-sum [range] (sum (unfold1 range)))

(defn all-the-same? [xs]
    (if (empty? xs)
        true
        (let* (frst (first xs)) 
            (for-all? (fn* [item] (= item frst)) xs)
        )
    )
)

(defn expand [input]
    (if (> (count input) 1)
        (let* (end (/ (count input) 2))
            (map (fn* [s] (map join (chunks input s))) (generate 1 end))
        )
        ()
    )
)

(defn is-repeated2? [input]
    (not 
        (empty?
            (filter all-the-same? (expand (seq input)))
        )
    )
)

(defn is-invalid2? [number]
    (let* (s (toString number)) (is-repeated2? s))
)

(defn unfold2 [range]
    (filter is-invalid2? (generate (first range) (second range)))
)

(defn unfold2-sum [range] (sum (unfold2 range)))

(def! input (if-defined DEBUG-EVAL "input/day2-test.txt" "input/day2.txt"))

; part1
(println
    (timed! (sum (map unfold1-sum (parse input))))
)

; part2
(println
    (timed! (sum (map unfold2-sum (parse input))))
)