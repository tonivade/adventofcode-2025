; https://adventofcode.com/2025/day/5

(import java.lang.String lines)
(import java.lang.String split 1)
(import java.lang.Long parseLong 1)

(load-file "mal/aoc.mal")

(def! input (if-defined DEBUG-EVAL "input/day5-test.txt" "input/day5.txt"))

(defn to-range (input)
    (map parseLong (split input "-"))
)

(defn parse [file]
    (let* (input (split (slurp file) "\n\n"))
        (let* (ranges (first input) products (second input))
            (list (map to-range (lines ranges)) (map parseLong (lines products)))
        )
    )
)

(defn in-range [range num]
   (and (>= num (first range)) (<= num (second range)))
)

(defn range-size [range]
    (incr (- (second range) (first range)))
)

(defn overlaps [left right]
    (and (>= (second left) (first right)) (<= (first left) (second right)))
)

(defn merge [left right]
    (list 
        (if (< (first left) (first right)) (first left) (first right))
        (if (> (second left) (second right)) (second left) (second right))
    )
)

(defn is-fresh [ranges product]
    (exists?
        (fn* [r] (in-range r product))
        ranges
    )
)

(defn count-fresh [ranges products]
    (count
        (filter
            (fn* [p] (is-fresh ranges p))
            products
        )
    )
)

(defn merge-ranges [ranges]
    (let* (sorted-ranges (sort-list-by (fn* [a b] (< (first a) (first b))) ranges))
        (reduce 
            ()
            (fn* [range acc] 
                (let* 
                    (group (index-where acc (fn* [r] (overlaps range r))))
                    (if (nil? group)
                        (cons range acc)
                        (replace-nth acc group (merge (nth acc group) range))
                    )
                )
            )
            sorted-ranges
        )
    )
)

(println 
    (timed! 
        (let* (parsed (parse input))
            (count-fresh (first parsed) (second parsed))
        )
    )
)

(println 
    (timed! 
        (let* (parsed (parse input))
            (sum
                (map 
                    range-size
                    (merge-ranges (first parsed)))
            )
        )
    )
)